apply plugin: 'com.android.library'

android {
    compileSdkVersion var.compileSdkVersion
    buildToolsVersion var.buildToolsVersion

    defaultConfig {
        minSdkVersion var.minSdkVersion
        targetSdkVersion var.targetSdkVersion
        versionCode 34
        versionName "1.3.4"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true//配置5.0 版本以下可以使用svg

        ndk {
            // 设置支持的SO库架构
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    testImplementation 'junit:junit:4.12'
    api "com.android.support:appcompat-v7:$var.supportLibraryVersion"
    api "com.android.support:recyclerview-v7:$var.supportLibraryVersion"
    api "com.android.support:cardview-v7:$var.supportLibraryVersion"
    api "com.android.support:design:$var.supportLibraryVersion"
    api "com.android.support:exifinterface:$var.supportLibraryVersion"
    api 'com.android.support.constraint:constraint-layout:1.1.2'

    //    butterknife
    api 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //    dagger2
    api 'com.google.dagger:dagger:2.9'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.9'

    //    rxjava
    api 'io.reactivex.rxjava2:rxjava:2.1.9'
    api 'io.reactivex.rxjava2:rxandroid:2.0.2'

    //    retrofit2
    api 'com.squareup.retrofit2:retrofit:2.3.0'

    //    为retrofit添加gson转换器的依赖
    api 'com.squareup.retrofit2:converter-gson:2.3.0'

    //    Retrofit与RxJava结合
    api 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    //    通过OkHttp中的interceptor来实现实际的底层的请求和响应日志
    api 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    //    glide
    api 'com.github.bumptech.glide:glide:4.7.1'
    api 'com.github.bumptech.glide:okhttp3-integration:4.7.1'
    api 'com.github.bumptech.glide:annotations:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'

    //    //circleimageview 圆形图片

    //    api 'de.hdodenhof:circleimageview:2.1.0'

    //    //baseRecyclerViewAdapterHelper

    //    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'

    //    //下拉刷新上拉加载

    //    api 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.3'

    //    //PhotoView

    //    api 'com.github.chrisbanes:PhotoView:2.0.0'

    //    //takephoto

    //    api 'com.jph.takephoto:takephoto_library:4.0.3'

    //    //性能更好的MD风格的RatingBar

    //    api 'me.zhanghai.android.materialratingbar:library:1.2.0'
//    aop
    api 'org.aspectj:aspectjrt:1.8.13'
}


/** 以下开始是将Android Library上传到jcenter的相关配置**/
apply plugin: 'com.novoda.bintray-release'

allprojects {
    tasks.withType(Javadoc) {
        options{
            encoding "UTF-8"
            charSet 'UTF-8'
            links "http://docs.oracle.com/javase/7/docs/api"
        }
    }
}

publish {
    //bintray.com用户名
    userOrg = 'fy310518'
    //要传到的maven的名字。你可能有多个maven，要传哪个写哪个
    repoName= 'grace'
    //jcenter上的路径
    groupId = 'com.fy.grace'
    //项目名称
    artifactId = 'baselibrary'
    //版本号
    publishVersion = '1.3.4'
    //描述，不重要
    desc = 'Elegant Android application development base library'
    //网站，不重要
    website = 'https://github.com/fy310518/wanAndroid'
}

//gradlew clean build bintrayUpload -PbintrayUser=fy310518 -PbintrayKey=54c46159b02281fdabc355fb3912291e0d390994 -PdryRun=false
//测试 是否上传成功
//https://jcenter.bintray.com/fy310518.maven


/** 以下开始是 Android aop 相关配置**/
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.aspectj:aspectjtools:1.8.13'
        classpath 'org.aspectj:aspectjweaver:1.8.13'
    }
}

import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

android.libraryVariants.all { variant ->
    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.5",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", android.bootClasspath.join(
                File.pathSeparator)]

        MessageHandler handler = new MessageHandler(true)
        new Main().run(args, handler)

        def log = project.logger
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break
                case IMessage.WARNING:
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break
            }
        }
    }
}